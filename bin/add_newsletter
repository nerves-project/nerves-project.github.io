#! /usr/bin/env elixir

usage = """
This script is used to fetch the contents of a newsletter and create the resources
required to archive it on the website.

It requires the URL to the newsletter source HTML, which can typically be found
in the email at the top where it says "No images? Click here"

You can optionally include the issue number and date. Otherwise they inferred as the
next incrementing number and todays current date.

./bin/add_newsletter [URL] --date %Y-%m-%d --number n
"""

## Dependencies
Mix.install([
  {:req, "~> 0.3"},
  {:floki, "~> 0.34"}
])

{opts, [url | _]} = OptionParser.parse!(System.argv(), strict: [date: :string, help: :boolean, number: :integer])

if opts[:help] do
  IO.puts(usage)
  System.halt(0)
end

unless String.starts_with?(url, "http") do
  IO.puts [IO.ANSI.red(), "Must include a valid URL to the newsletter", IO.ANSI.default_color()]
  System.halt(1)
end

root = File.cwd!

unless Path.basename(root) == "nerves-project.github.io" do
  IO.puts [IO.ANSI.red(), "add_newsletter must be run from the repo root (i.e. ./bin/add_newsletter)", IO.ANSI.default_color()]
  System.halt(1)
end

content = Path.join(root, "content/newsletter")

next =
  if opts[:number] do
    opts[:number]
  else
      IO.puts "|- Determining next issue"
      issues =
        for path <- Path.wildcard([content, "/2*"]),
            str = File.read!(path),
            [num_str] = Regex.run(~r/aliases:\r?\n  - (\d+)/, str, capture: :all_but_first),
            do: String.to_integer(num_str)

      Enum.max(issues) + 1
  end
IO.puts("|- Adding issue #{to_string(next)}")

IO.puts("|- Fetching HTML")
%{body: body} = Req.get!(url)

[title] = Floki.parse_document!(body) |> Floki.attribute("meta[property='og:title']", "content")

date = if opts[:date] && opts[:date] =~ ~r/^\d{4}-\d{2}-\d{2}$/ do
  opts[:date]
else
  Date.utc_today() |> Calendar.strftime("%Y-%m-%d")
end

html_name = Path.join(content, [date, "-", String.replace(title, " ", "-") |> String.downcase(), ".html"])
data = """
---
title: #{title}
date: #{date}
aliases:
  - #{next}
---
"""

IO.puts(["|- Writing ", Path.relative_to(html_name, root)])
File.write!(html_name, [data, "\n", body])

IO.puts([IO.ANSI.green(), "|- Done!", IO.ANSI.default_color()])
